#
# Copyright (C) 2022-2024 LAAS-CNRS, INRIA
#

cmake_policy(SET CMP0057 NEW)

# Create an example
function(create_example exfile)
  set(options "")
  set(oneValueArgs "")
  set(multiValueArgs DEPENDENCIES)

  cmake_parse_arguments(
    arg_crex
    "${options}"
    "${oneValueArgs}"
    "${multiValueArgs}"
    ${ARGN}
  )

  get_filename_component(exname ${exfile} NAME_WE)
  set(exname "${PROJECT_NAME}-example-${exname}")
  create_ex_or_bench(${exfile} ${exname} DEPENDENCIES ${arg_crex_DEPENDENCIES})
endfunction()

create_example(clqr.cpp)

if(PROXSUITE_NLP_WITH_PINOCCHIO_SUPPORT)
  create_example(se2-car.cpp)
endif()

if(PINOCCHIO_V3)
  create_example(talos-walk.cpp DEPENDENCIES talos_walk_utils)
endif()

if(BUILD_CROCODDYL_COMPAT)
  ADD_PROJECT_PRIVATE_DEPENDENCY(example-robot-data 4.0.9 REQUIRED)
  create_example(
    talos-arm.cpp
    DEPENDENCIES aligator::croc_compat croc_talos_arm_utils
  )
endif()

file(GLOB EXAMPLES_PY *.py)

foreach(pyfile ${EXAMPLES_PY})
  get_filename_component(pyfile_name ${pyfile} NAME)
  execute_process(
    COMMAND
      ${CMAKE_COMMAND} -E create_symlink ${pyfile}
      ${CMAKE_CURRENT_BINARY_DIR}/${pyfile_name}
  )
endforeach()
